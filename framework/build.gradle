/*
 * This Work is in the public domain and is provided on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied,
 * including, without limitation, any warranties or conditions of TITLE,
 * NON-INFRINGEMENT, MERCHANTABILITY, or FITNESS FOR A PARTICULAR PURPOSE.
 * You are solely responsible for determining the appropriateness of using
 * this Work and assume any risks associated with your use of this Work.
 *
 * This Work includes contributions authored by David E. Jones, not as a
 * "work for hire", who hereby disclaims any copyright to the same.
 */

apply plugin: 'groovy'
apply plugin: 'war'

sourceCompatibility = '1.6'
archivesBaseName = 'moqui'

sourceSets {
    start
    api
}

// maybe in the future: repositories { mavenCentral() }
repositories {
    flatDir name: 'localLib', dirs: projectDir.absolutePath + '/lib'
    flatDir name: 'localLibFop', dirs: projectDir.absolutePath + '/lib/fop'
}

dependencies {
    compile name: 'antisamy', version: '1.4'

    compile module(name: 'atomikos-transactions', version: '3.7.0') {
        dependencies module(name: 'atomikos-util', version: '3.7.0') }
    compile name: 'atomikos-transactions-api', version: '3.7.0'
    compile name: 'atomikos-transactions-jdbc', version: '3.7.0'
    // may use in the future: runtime name: 'atomikos-transactions-jms', version: '3.7.0'
    compile name: 'atomikos-transactions-jta', version: '3.7.0'

    compile name: 'commons-codec', version: '1.4'
    compile name: 'commons-collections', version: '3.2.1'
    compile name: 'commons-email', version: '1.2'
    compile name: 'commons-fileupload', version: '1.2.2'
    compile name: 'commons-httpclient', version: '4.1'
    compile name: 'commons-httpcore', version: '4.1'
    compile name: 'commons-io', version: '2.0.1'
    compile name: 'commons-validator', version: '1.3.1'

    compile name: 'ehcache-core', version: '2.4.3'
    compile name: 'esapi', version: '2.0GA'
    compile name: 'freemarker', version: '2.3.18'

    compile name: 'geronimo-j2ee-connector_1.5_spec', version: ''
    compile name: 'geronimo-jms_1.1_spec', version: '1.0'
    compile name: 'geronimo-jta_1.1_spec', version: '1.1.1'

    compile name: 'groovy-all', version: '1.8.0'

    compile name: 'jackrabbit-api', version: '2.2.2'
    compile name: 'jackrabbit-jcr-commons', version: '2.2.2'
    compile name: 'jcr', version: '2.0'

    compile name: 'jsonrpc2-base', version: '1.20'
    compile name: 'jsonrpc2-client', version: '1.4'
    compile name: 'jsonrpc2-server', version: '1.4.1'

    compile name: 'jsr173_1.0_api', version: ''
    compile name: 'liquibase', version: '2.0.1'
    compile name: 'mail', version: ''
    compile name: 'org.eclipse.mylyn.wikitext.core_1.6.0.I20110830-0427', version: ''
    compile name: 'org.eclipse.mylyn.wikitext.confluence.core_1.6.0.I20110830-0427', version: ''
    compile name: 'org.eclipse.mylyn.wikitext.mediawiki.core_1.6.0.I20110830-0427', version: ''
    compile name: 'org.eclipse.mylyn.wikitext.textile.core_1.6.0.I20110830-0427', version: ''
    compile name: 'org.eclipse.mylyn.wikitext.tracwiki.core_1.6.0.I20110830-0427', version: ''
    compile name: 'org.eclipse.mylyn.wikitext.twiki.core_1.6.0.I20110830-0427', version: ''

    compile name: 'quartz-all', version: '2.0.2'

    compile name: 'redstone-xmlrpc', version: '1.1.1'
    compile name: 'redstone-xmlrpc-client', version: '1.1.1'

    compile module(name: 'shiro-core', version: '1.1.0') {
        dependencies module(name: 'commons-beanutils', version: '1.8.3'), module(name: 'commons-logging', version: '1.1.1') }
    compile name: 'shiro-web', version: '1.1.0'

    compile module(name: 'slf4j-api', version: '1.6.0') {
        dependencies module(name: 'log4j', version: '1.2.15'), module(name: 'slf4j-log4j12', version: '1.6.0') }

    // winstone is in providedCompile so that it won't be included in the war file WEB-INF/lib
    providedCompile name: 'winstone', version: '0.9.10'

    compile module(name: 'fop', version: '1.0') {
        dependencies module(name: 'avalon-framework', version: '4.2.0'), module(name: 'xmlgraphics-commons', version: '1.4')
    }

    // groovy dependencies (just the library to compile with?)
    groovy name: 'groovy-all', version: '1.8.0'

    // ========== runtime-only dependencies ==========
    runtime name: 'derby', version: '10.8.1.2'
    runtime name: 'jakarta-oro', version: '2.0.8'
    runtime name: 'shiro-ehcache', version: '1.1.0'
    runtime name: 'shiro-quartz', version: '1.1.0'
}

task apiJavadoc(type: Javadoc) {
    source sourceSets.api.allJava
    classpath = sourceSets.main.compileClasspath
    destinationDir = file(docsDir.absolutePath + '/apiJavadoc')
}

// setup task dependencies to make sure the start and api sourceSets always get run
compileJava.dependsOn startClasses
compileJava.dependsOn apiClasses
// add the classes directory for the api SourceSet to the main compile classpath so that dependent classes are available
sourceSets.main.compileClasspath += files(sourceSets.api.classesDir)

jar {
    // this is necessary otherwise jar won't build when war plugin is applied
    enabled = true
    appendix = 'framework'
    manifest { attributes 'Implementation-Title': 'Moqui Framework', 'Implementation-Version': version }
    from sourceSets.api.classes
    from sourceSets.main.classes
    // get all of the "resources" that are in component-standard directories instead of src/main/resources
    from fileTree(dir: projectDir.absolutePath,
            includes: ['data/**', 'entity/**', 'screen/**', 'service/**', 'template/**', 'xsd/**'])
}

war {
    dependsOn jar
    // put the war file in the parent directory, ie the moqui dir instead of the framework dir
    destinationDir = file(projectDir.absolutePath + '/..')
    // add MoquiInit.properties to the WEB-INF/classes dir for the deployed war mode of operation
    from(fileTree(dir: projectDir.absolutePath + '/..', includes: ['MoquiInit.properties'])) { into 'WEB-INF/classes' }
    // this excludes the classes in sourceSets.main.classes (better to have the jar file built above)
    classpath = configurations.runtime - configurations.providedCompile
    classpath file(jar.archivePath)

    // put start classes and winstone in the root of the war file for the executable war/jar mode of operation
    from sourceSets.start.classes
    from file(projectDir.absolutePath + '/lib/winstone-0.9.10.jar')
    // setup the manifest for the executable war/jar mode
    manifest { attributes 'Implementation-Title': 'Moqui Start', 'Implementation-Vendor': 'Moqui Framework',
            'Implementation-Version': version, 'Main-Class': 'MoquiStart' }
}
